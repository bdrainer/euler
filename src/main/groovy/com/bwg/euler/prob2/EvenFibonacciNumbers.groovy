package com.bwg.euler.prob2

import com.bwg.euler.util.Fibonacci
import com.bwg.euler.util.Validator
import groovy.time.TimeCategory
import groovy.util.logging.Log
import org.springframework.beans.factory.annotation.Autowired
import org.springframework.web.bind.annotation.GetMapping
import org.springframework.web.bind.annotation.PathVariable
import org.springframework.web.bind.annotation.RestController
import reactor.core.publisher.Mono

import static org.springframework.http.MediaType.APPLICATION_JSON_VALUE

/**
 * Problem 2 - Even Fibonacci Numbers
 *
 * Each new term in the Fibonacci sequence is generated by adding the previous two terms.
 * By starting with 1 and 2, the first 10 terms will be: 1, 2, 3, 5, 8, 13, 21, 34, 55, 89, ...
 *
 * By considering the terms in the Fibonacci sequence whose values do not exceed four
 * million, find the sum of the even-valued terms.
 *
 * Answer: 4613732
 */
@Log
@RestController
class EvenFibonacciNumbers {

    private static final BigInteger LIMIT = 4000000

    @Autowired
    Fibonacci fib

    @GetMapping(value = "/evenfibnumbers/{value}", produces = APPLICATION_JSON_VALUE)
    def calculate(@PathVariable BigInteger value) {
        def start = new Date()

        log.info("Finding the even fibonacci numbers sum for $value")
        Validator.checkExceedsMaxValue(value, LIMIT)
                .map { ex -> Mono.error(ex) }
                .orElseGet {
                    def sum = fib.evenSum(value)
                    log.info "Problem 2 execution time: ${TimeCategory.minus(new Date(), start)}"
                    Mono.just sum
                }
    }

}
